#lang racket/base
(require compiler/zo-parse)

(define (check-phaseless is? form)
  (parameterize ([current-namespace (make-base-namespace)])
    (define o (open-output-bytes))
    (write (compile `(module m racket/kernel ,form)) o)
    (close-output-port o)
    (define i (open-input-bytes (get-output-bytes o)))
    (define e (zo-parse i))
    (unless (equal? is? (and (memq 'phaseless (mod-flags (compilation-top-code e))) #t))
      (error 'phaseless "failed: ~s ~s\n" is? form))))

(check-phaseless #t '(define-values (x) 5))
(check-phaseless #t '(define-values (x) '5))
(check-phaseless #t '(define-values (x) (#%datum . 5)))
(check-phaseless #t '(define-values (x) #t))
(check-phaseless #t '(define-values (x) 'x))
(check-phaseless #t '(define-values (x) "x"))
(check-phaseless #t '(define-values (x) #"x"))
(check-phaseless #t '(define-values (x) cons))
(check-phaseless #t '(define-values (x) (cons 1 2)))
(check-phaseless #t '(define-values (x) (list 1 2)))
(check-phaseless #t '(define-values (x) (cons 1 '())))
(check-phaseless #t '(#%require racket/private/stx))
(check-phaseless #t '(define-values (x) (lambda (x) x)))
(check-phaseless #t '(define-values (x) (case-lambda [(x) x] [y y])))
(check-phaseless #t '(define-values (struct: ? -ref) (make-struct-type-property 'p)))
(check-phaseless #t '(define-values (struct: make- ? -ref -set!) (make-struct-type 's #f 0 0)))
(check-phaseless #t '(define-values (struct: make- ? -ref -set!) (make-struct-type 's struct:exn 0 0)))
(check-phaseless #t '(define-values (struct: make- ? -ref -set!) (make-struct-type 's struct:exn 1 0 #f (list (cons prop:procedure 0)))))
(check-phaseless #t '(begin
                       (define-values (x) 5)
                       (define-values (y) 6)))

(check-phaseless #f '(define-values (x) #(x)))
(check-phaseless #f '(define-values (x) '(x)))
(check-phaseless #f '(define-values (x) (quote-syntax x)))
(check-phaseless #f '(define-values (x) (lambda () (quote-syntax x))))
(check-phaseless #f '(define-values (x) (#%variable-reference)))
(check-phaseless #f '(define-values (x) (lambda () (#%variable-reference))))
(check-phaseless #f '(define-values (x) (lambda () (if #f (#%variable-reference) 10))))
(check-phaseless #f '(define-values (x) (#%variable-reference x)))
(check-phaseless #f '(#%require racket/base))

(check-phaseless #t '(module* sub #f (vector 1 2 3)))
(check-phaseless #t '(module* sub #f (#%variable-reference)))
(check-phaseless #t '(module* sub racket/base (#%variable-reference)))
(check-phaseless #t '(module sub racket/base (#%variable-reference)))

;; Check phase crossing via an exception:
(parameterize ([current-namespace (make-base-namespace)])
  (eval `(module m racket/kernel
           (#%provide s? make-s)
           (define-values (struct:s make-s s? s-ref s-set!) (make-struct-type 's #f 0 0))))
  (eval '(require 'm))
  (define s? (eval 's?))
  (eval '(require (for-syntax racket/base 'm)))
  (eval '(define-syntax (m stx) (raise (make-s))))
  (with-handlers ([s? void])
    (eval '(m)))
  (define (check-exn)
    (with-handlers ([s? void])
      (eval '(module n racket/base
               (require (for-syntax racket/base 'm))
               (begin-for-syntax
                (raise (make-s)))))))
  (check-exn)
  (define (check-attach namespace-attach-module)
    (define ns (make-base-namespace))
    (namespace-attach-module (current-namespace) ''m ns)
    (parameterize ([current-namespace ns])
      (check-exn)))
  (check-attach namespace-attach-module)
  (check-attach namespace-attach-module-declaration))

(displayln "All tests passed.")

